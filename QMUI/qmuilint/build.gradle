apply plugin: 'com.android.library'

group = 'com.qmuiteam'
version = "1.0.1" // QMUI Lint 发布到 bintray 的版本号

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        minSdkVersion 14
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

configurations {
    lintChecks
}

dependencies {
    lintChecks project(path: ':QMUI:qmuilintrule', configuration: 'lintChecks')
}

task copyLintJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

// deploy
File deployConfig = project.file('deploy.properties')
if (deployConfig.exists()) {
    apply from: "deployMaven.gradle"
    apply from: "deployBintray.gradle"
}
